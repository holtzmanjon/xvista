	SUBROUTINE VTOPEN
	INCLUDE 'IODEF.PAR/NOLIST'
	INCLUDE 'SSDEF.PAR/NOLIST'

	INTEGER*4 SYS$ASSIGN, VTCHN, SYS$QIOW, RETCOD
	INTEGER*2 IOSB(4)
	COMMON /VT/ VTCHN, RETCOD, IOSB
	
	RETCOD = SYS$ASSIGN('VTNAME',VTCHN,,)
	IF(RETCOD.NE.SS$_NORMAL) THEN
		WRITE(6,1002) RETCOD
1002		FORMAT(1X,' Problem with channel assignment - SS_code =',Z6)
		CALL ERRMSG(RETCOD)
		CALL EXIT
	ENDIF

	RETURN
	END

	SUBROUTINE VTCLOSE

	INCLUDE 'IODEF.PAR/NOLIST'
	INCLUDE 'SSDEF.PAR/NOLIST'

	INTEGER*4 SYS$ASSIGN, VTCHN, SYS$QIOW, RETCOD
	INTEGER*2 IOSB(4)
	COMMON /VT/ VTCHN, RETCOD, IOSB
	
	CALL SYS$DASSGN(VTCHN)
	RETURN
	END

	SUBROUTINE VTLINE(NLINE,BUF)
	INCLUDE 'IODEF.PAR/NOLIST'
	INCLUDE 'SSDEF.PAR/NOLIST'
	INTEGER*4 IO$M_PLOT /Z40/	!function modifier for plot call
	INTEGER*4 IO$M_PASSALL /Z100/	!passall mode usefull for plotting
	INTEGER*4 IOMODIFY /Z140/	!above two ored together
	INTEGER*4 IOCC /0/		!carriage control block

	INTEGER*4 SYS$ASSIGN, VTCHN, SYS$QIOW, RETCOD
	INTEGER*2 IOSB(4)
	COMMON /VT/ VTCHN, RETCOD, IOSB

	BYTE BUF(1)

	IOFUNC = IOR(IO$_WRITEVBLK,IOMODIFY)
	NBYTE = NLINE*264
	RETCOD = SYS$QIOW( ,%VAL(VTCHN),%VAL(IOFUNC),IOSB, , ,
	1      %REF(BUF),%VAL(NBYTE), ,IOCC, , )
	IF(RETCOD.NE.SS$_NORMAL) THEN
		WRITE(6,1000) RETCOD
1000		FORMAT(' QIO ret code - SS_code =',Z6)
		CALL ERRMSG(RETCOD)
		RETURN
	ENDIF
	IF(IOSB(1).NE.SS$_NORMAL.AND.IOSB(1).NE.SS$_ENDOFFILE) THEN
		WRITE(6,1001) IOSB(1),SS$_NORMAL
1001		FORMAT(' VT error - SS_code =',2Z6)
		CALL ERRMSG(IOSB(1))
		RETURN
	ENDIF
	RETURN
	END

	SUBROUTINE VTSPACE(N)
	BYTE BUF(264) /264*0/

	DO 10 J = 1,N
	CALL VTLINE(1,BUF)
10	CONTINUE

	RETURN
	END

	SUBROUTINE ERRMSG(ICODE)
	CHARACTER*256 MSGBUF
	CALL SYS$GETMSG(%VAL(ICODE),MSGLEN,%DESCR(MSGBUF),%VAL(15),)
	print *, MSGBUF(1:MSGLEN)
	RETURN
	END

	SUBROUTINE VTFF
	OPEN(UNIT=93,NAME='VTNAME',STATUS='UNKNOWN')
	WRITE(93,1000)
1000	FORMAT('1')
	CLOSE(93)
	RETURN
	END
