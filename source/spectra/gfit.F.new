#include "Vista.h"

      SUBROUTINE GFIT(A,IMAGESR,IMAGEER,IMAGESC,IMAGEEC)

C
C  GFIT - Fit gaussian to spectrum
C
C---------------------------------------------------------------------------

      REAL*4 A(IMAGESC:IMAGEEC,IMAGESR:IMAGEER)

#ifdef VMS
      INCLUDE 'VINCLUDE:VISTALINK.INC'
      INCLUDE 'VINCLUDE:IMAGELINK.INC'
      INCLUDE 'VINCLUDE:WORK.INC'
#else 
      INCLUDE 'vistadisk/source/include/vistalink.inc'
      INCLUDE 'vistadisk/source/include/imagelink.inc'
      INCLUDE 'vistadisk/source/include/work.inc'
#endif
      LOGICAL HAVEBOX, KEYCHECK, LOAD, VERBOSE, ABSORP, HAVEFILE
      LOGICAL FITCONT, GLLS, HAVEWID
      INTEGER SR, ER, SC, EC, IC1, IC2, IC3, IC4
      PARAMETER(MAXPAR=7)
      REAL PAR(MAXPAR), LAMBDA
      REAL BETA(MAXPAR), ALPHA(MAXPAR,MAXPAR), DEL(MAXPAR), DRV(MAXPAR)
      REAL BETAOLD(MAXPAR), ALPHAOLD(MAXPAR,MAXPAR), PAROLD(MAXPAR)
      CHARACTER PARM*8, LINEFILE*64, TWORD*80, LINE*80
      PARAMETER (MAXPTS = 100, MAXLINES=2)
      REAL*8 XFIT(MAXPTS), YFIT(MAXPTS), W(MAXPTS)
      REAL*8 LPAR(2), DPAR(2), WORK(4), VAR
      REAL*8 PEAK(MAXLINES), CENT(MAXLINES), WID(MAXLINES), F(MAXLINES)
      REAL*8 B1, B2, B3, B4, WAVE(MAXLINES), FPOLY
      EXTERNAL FPOLY
      DOUBLE PRECISION LAMPIX, PIXLAM

C  List allowed keywords.
      CALL KEYINIT
      CALL KEYDEF('BOX=')
      CALL KEYDEF('LINES=')
      CALL KEYDEF('MAXITER=')
      CALL KEYDEF('GAIN=')
      CALL KEYDEF('RN=')
      CALL KEYDEF('WID=')
      CALL KEYDEF('LOAD')
      CALL KEYDEF('VERBOSE')
      CALL KEYDEF('ABSORP')

C  Set up default values.
      HAVEBOX = .FALSE.
      LOAD = .FALSE.
      ABSORP = .FALSE.
      VERBOSE = .FALSE.
      HAVEFILE = .FALSE.
      HAVEWID = .FALSE.
      MAXITER = 100
      GAIN=1
      RN=0
      DO 8701 I=1, NCON
         TWORD = WORD(I)
         L = UPPER(TWORD)
         IF (TWORD(1:4).EQ.'BOX=') THEN
            CALL ASSIGN(WORD(I),FC,PARM)
            IF (XERR) RETURN
            IBN = NINT(FC)
            HAVEBOX = .TRUE.
         ELSE IF (TWORD(1:8).EQ.'MAXITER=') THEN
            CALL ASSIGN(WORD(I),FC,PARM)
            IF (XERR) RETURN
            MAXITER = NINT(FC)
         ELSE IF (TWORD(1:5).EQ.'GAIN=') THEN
            CALL ASSIGN(WORD(I),GAIN,PARM)
            IF (XERR) RETURN
         ELSE IF (TWORD(1:3).EQ.'RN=') THEN
            CALL ASSIGN(WORD(I),RN,PARM)
            IF (XERR) RETURN
         ELSE IF (TWORD(1:4).EQ.'WID=') THEN
            CALL ASSIGN(WORD(I),WIDINIT,PARM)
            IF (XERR) RETURN
         ELSE IF (TWORD.EQ.'ABSORP') THEN
            ABSORP = .TRUE.
         ELSE IF (TWORD.EQ.'LOAD') THEN
            LOAD = .TRUE.
         ELSE IF (TWORD.EQ.'VERBOSE') THEN
            VERBOSE = .TRUE.
         ELSE IF (TWORD(1:6).EQ.'LINES=') THEN
            LINEFILE = WORD(I)(7:)
            HAVEFILE = .TRUE.
         END IF
 8701 CONTINUE

C  Exit if we were given a keyword we don''t understand.
      IF (.NOT. KEYCHECK()) THEN
         XERR = .TRUE.
         RETURN
      END IF

      ISP = IM
      IF (HAVEBOX) THEN
        CALL GETBOX(IBN,ICOORD(1,IM),SR,ER,SC,EC)
        IF (XERR) RETURN
      ELSE
        SR = IMAGESR
        ER = IMAGEER
        SC = IMAGESC
        EC = IMAGEEC
      END IF

      IF (HAVEFILE) THEN
        L = NUMCHAR(LINEFILE)

        OPEN(1,FILE=LINEFILE(1:l),STATUS='old',IOSTAT=IERR)
        IF (IERR .NE. 0) THEN
          PRINT *, 'Error opening file: ', LINEFILE
          XERR = .TRUE.
          RETURN
        END IF
      END IF

1     CONTINUE
      IF (HAVEFILE) THEN
        READ(1,'(A)',END=99) LINE
        READ(LINE,*,END=2,ERR=2) WAVE(1), S, S, S, WID(1), B1, B2, B3, B4, WAVE(2)
        NLINES=2
        GOTO 3
2       READ(LINE,*,END=99) WAVE(1), S, S, S, WID(1), B1, B2, B3, B4
        NLINES = 1
3       CONTINUE
        SC = INT(PIXLAM(WAVE(1)-WID(1), ISP))
        EC = INT(PIXLAM(WAVE(1)+WID(1), ISP))+1
      ELSE
        B1=-1
        B2=-1
        B3=-1
        B4=-1
        NLINES = 1
      END IF

      IF (B1 .GT. 0) THEN
        IC1 = INT(PIXLAM(B1,ISP))
        IC2 = INT(PIXLAM(B2,ISP))+1
        IC3 = INT(PIXLAM(B3,ISP))
        IC4 = INT(PIXLAM(B4,ISP))+1
        FITCONT = .FALSE.
        NPTS = 0
        DO IX=IC1,IC2
          NPTS=NPTS+1
          XFIT(NPTS) = LAMPIX(DBLE(IX),ISP)-WAVE(1)
          YFIT(NPTS) = A(IX,SR)
          W(NPTS) = 1.
        END DO
        DO IX=IC3,IC4
          NPTS=NPTS+1
          XFIT(NPTS) = LAMPIX(DBLE(IX),ISP)-WAVE(1)
          YFIT(NPTS) = A(IX,SR)
          W(NPTS) = 1.
        END DO
        IF (.NOT. GLLS(XFIT,YFIT,W,NPTS,LPAR,2,DPAR,WORK,VAR,FPOLY,.FALSE.)) THEN
          PRINT *, 'Error in GLLS'
          XERR = .TRUE.
          RETURN
        END IF 
        PRINT *, 'Continuum fit: ', LPAR(1), LPAR(2)
      ELSE
        FITCONT = .TRUE.
      END IF
C Get initial guess of  parameters
C   par(1) = gaussian intensity
C   par(2) = center
C   par(3) = width
C   par(4) = sky

      DO ILINE = 1, NLINES
        IF (ABSORP) THEN
          CONT = -1E10
          PEAK(ILINE) = 1E10
        ELSE
          CONT = 1E10
          PEAK(ILINE) = -1E10
        END IF
      END DO
C   Continuum
      IF (FITCONT) THEN
        DO IX=SC,EC
          VAL=A(IX,SR)
          IF (ABSORP) THEN
            IF (VAL .GT. CONT) CONT = VAL
          ELSE
            IF (VAL .LT. CONT) CONT = VAL
          END IF
        END DO
      ELSE
        CONT = LPAR(1)
      END IF

C  Line parameters
      DO ILINE = 1, NLINES
        IF (HAVEWID) THEN
          ISC = INT(PIXLAM(WAVE(ILINE), ISP))-WIDINIT
          IEC = INT(PIXLAM(WAVE(ILINE), ISP))+WIDINIT
        ELSE
          ISC = SC
          IEC = EC
        END IF

C   Line center
        TOT = 0.
        TOT2 = 0.
        TOTINTEN = 0.
        DO IX=SC,EC
          VAL=A(IX,SR)-CONT
          IF (ABSORP) VAL = -1 * VAL
          TOT = TOT +  VAL*IX
          TOT2 = TOT2 + VAL*IX**2
          TOTINTEN = TOTINTEN + VAL
        END DO
        CENT(ILINE) = TOT/TOTINTEN

C        PAR(3) = TOT2/TOTINTEN - PAR(2)**2
  
        DO IX=ISC,IEC
          VAL=A(IX,SR)
          IF (ABSORP) THEN
            IF (VAL .LT. PEAK(ILINE)) PEAK(ILINE) = VAL
          ELSE
            IF (VAL .GT. PEAK(ILINE)) PEAK(ILINE) = VAL
          END IF
        END DO
        IF (ABSORP) THEN
          PEAK(ILINE) = CONT - PEAK(ILINE)
        ELSE
          PEAK(ILINE) = PEAK(ILINE) - CONT
        END IF
  
        IF (HAVEWID) THEN
          WID(ILINE) = WIDINIT
        ELSE
          DO IX=ISC,IEC
            VAL=A(IX,SR)-PAR(4)
            IF (ABSORP) VAL = -1 * VAL
            IF (VAL .GT. 0.5*PEAK(ILINE)) WID(ILINE) = ABS(IX-CENT(ILINE))
          END DO
        END IF
      END DO

      IPAR = 0
      DO ILINE = 1, NLINES
        IPAR = IPAR + 1
        PAR(IPAR) = PEAK(ILINE)
        IPAR = IPAR + 1
        PAR(IPAR) = CENT(ILINE)
        IPAR = IPAR + 1
        PAR(IPAR) = WID(ILINE)
      END DO
      IF (FITCONT) THEN
        IPAR = IPAR+ 1
        PAR(IPAR) = CONT
      END IF
      if (verbose)	print *, 'par: ', (par(i),i=1,ipar)

C  Fit function:  I = I_0 exp -0.5*(x-x0)**2/sig**2 + c
C    absorption:  I = c - I_0 exp -0.5*(x-x0)**2/sig**2
      NITER = 0
      LAMBDA = 1.E-6
      CHI2OLD = 0
101   CONTINUE
      NITER = NITER + 1
      IF (NOGO) GOTO 199
      NPAR = 3*NLINES
      IF (FITCONT) NPAR = NPAR + 1
      DO IPAR=1,NPAR
        BETA(IPAR) = 0.
        DO JPAR=1,NPAR
          ALPHA(JPAR,IPAR) = 0.
        END DO
      END DO
      CHI2 = 0
      DO IX=SC,EC
        VAL=A(IX,SR)
        SIG2 = MAX(0.,VAL)/GAIN+(RN/GAIN)**2
        FTOT = 0
        DO ILINE=1,NLINES
          F(ILINE) =  PEAK(ILINE)*EXP(-0.5*(IX-CENT(ILINE))**2/WID(ILINE)**2)
          FTOT = FTOT + F(ILINE)
        END DO
        IF (.NOT. FITCONT) 
     &     CONT = LPAR(1) + LPAR(2)*(LAMPIX(DBLE(IX),ISP)-WAVE(1))
        IF (ABSORP) THEN
          FTOT = CONT - FTOT
        ELSE
          FTOT = CONT + FTOT
        END IF
        CHI2 = CHI2 + (VAL-FTOT)**2/SIG2
        IPAR = 0
        DO ILINE=1,NLINES
          IPAR = IPAR + 1
          DRV(IPAR) = F(ILINE)/PEAK(ILINE)
          IPAR = IPAR + 1
          DRV(IPAR) = F(ILINE)*(IX-CENT(ILINE))/WID(ILINE)**2
          IPAR = IPAR + 1
          DRV(IPAR) = F(ILINE)*(IX-CENT(ILINE))**2/WID(ILINE)**3
        END DO
        IF (FITCONT) DRV(IPAR+1) = 1.
	if (verbose) print *, ix, (f(i),i=1,nlines), val, sig2, cont, (drv(ipar),ipar=1,npar)
        DO IPAR=1,NPAR
          BETA(IPAR) = BETA(IPAR) + (VAL-FTOT)*DRV(IPAR)/SIG2
          DO JPAR=1,NPAR
            ALPHA(JPAR,IPAR) = ALPHA(JPAR,IPAR) + DRV(IPAR)*DRV(JPAR)/SIG2
          END DO
        END DO
      END DO
	print 1001, niter, npar, chi2, chi2old, lambda, 
     &        (par(ipar),ipar=1,npar), gain, rn, (peak(iline),cent(iline),wid(iline),iline=1,nlines)
1001	format(2i4,7e12.3,2f8.2,7e12.3)
      
      IF (NITER .GT. 1 .AND. CHI2 .GT. CHI2OLD) THEN
        DO IPAR = 1, NPAR
          PAR(IPAR) = PAROLD(IPAR)
          BETA(IPAR) = BETAOLD(IPAR)
          DO JPAR = 1, NPAR
           ALPHA(JPAR,IPAR) = ALPHAOLD(JPAR,IPAR)
          END DO
        END DO
        LAMBDA = LAMBDA*10
        IF (LAMBDA .GT. 1E6) GOTO 199
      ELSE
        CHI2OLD = CHI2
        LAMBDA = MAX(1.E-6,LAMBDA/10.)
        DO IPAR = 1, NPAR
          PAROLD(IPAR) = PAR(IPAR)
          BETAOLD(IPAR) = BETA(IPAR)
          DO JPAR = 1, NPAR
           ALPHAOLD(JPAR,IPAR) = ALPHA(JPAR,IPAR)
          END DO
        END DO
      END IF

      DO IPAR=1,NPAR
        ALPHA(IPAR,IPAR) = ALPHA(IPAR,IPAR)*(1+LAMBDA)
      END DO
      CALL INVERS(ALPHA,MAXPAR,NPAR,ISTAT)
	if (istat .ne. 0) then
	  print *, 'Error inverting matrix'
	  goto 199
        end if
      CALL VMUL(ALPHA,MAXPAR,NPAR,BETA,DEL)

      DO IPAR = 1, NPAR
        PAR(IPAR) = PAR(IPAR) + DEL(IPAR)
C        IF (IPAR .EQ. 4 .AND. PAR(IPAR) .LT. 0) THEN
C          PAR(IPAR) = (PAR(IPAR) - DEL(IPAR) ) / 2.
C        END IF
      END DO
      IPAR = 0
      DO ILINE = 1, NLINES
        IPAR = IPAR + 1
        PEAK(ILINE) = PAR(IPAR)
        IPAR = IPAR + 1
        CENT(ILINE) = PAR(IPAR)
        IPAR = IPAR + 1
        WID(ILINE) = PAR(IPAR)
      END DO
      IF (FITCONT) THEN
        IPAR = IPAR+1
        CONT = PAR(IPAR)
      END IF

C  Iterate if called for
      if (niter .lt. maxiter)  goto 101

C Replace spectrum with fit
      do ix=sc,ec
        IF (.NOT. FITCONT) 
     &      CONT = LPAR(1) + LPAR(2)*(LAMPIX(DBLE(IX),ISP)-WAVE(1))
        FTOT = CONT
        DO ILINE = 1, NLINES
          IF (ABSORP) THEN
            FTOT = FTOT - PEAK(ILINE)*EXP(-0.5*(IX-CENT(ILINE))**2/WID(ILINE)**2)
          ELSE
            FTOT = FTOT + PEAK(ILINE)*EXP(-0.5*(IX-CENT(ILINE))**2/WID(ILINE)**2)
          END IF
        END DO
        A(IX,SR) = FTOT
      end do

      IF (FITCONT) THEN
	print *, (par(ipar),ipar=1,npar), lpar
      ELSE
	print *, (par(ipar),ipar=1,npar)
      END IF
      IF (LOAD) THEN
        CALL VARIABLE('C',PAR(4),.TRUE.)
        CALL VARIABLE('I0',PAR(1),.TRUE.)
        CALL VARIABLE('X0',PAR(2),.TRUE.)
        CALL VARIABLE('X0ERR',SQRT(ALPHA(2,2)),.TRUE.)
        CALL VARIABLE('SIG',PAR(3),.TRUE.)
        CALL VARIABLE('CHI2',CHI2,.TRUE.)
      END IF

      IF (HAVEFILE) THEN
        GOTO 1
      END IF

99    CONTINUE
      IF (HAVEFILE) CLOSE(1)

      RETURN
199   CONTINUE
      IF (HAVEFILE) CLOSE(1)
      XERR = .TRUE.
      RETURN
      END

